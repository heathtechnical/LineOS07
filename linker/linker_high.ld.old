OUTPUT_FORMAT("elf32-i386")

ENTRY (start)

PAGE_SIZE = 0x1000;

SECTIONS
{
   . = 0xC0100000;

   .text : AT(ADDR(.text) - 0xC0000000)
   {
      __KERNEL_START = .;
      __KERNEL_TEXT_START = .;
      code = .;
      *(.text)
      *(.rodata*)
      __KERNEL_TEXT_END = .;
   }

   .data ALIGN (0x1000) : AT(ADDR(.data) - 0xC0000000)
   {
      __KERNEL_DATA_START = .;
      data = .;
      *(.data)
      __KERNEL_DATA_END = .;
   }

   .bss : AT(ADDR(.bss) - 0xC0000000)
   {

      __KERNEL_BSS_START = .;
      bss = .;
      *(.bss)
      *(COMMON)
      __KERNEL_BSS_END = .;
   }
   . = ALIGN(PAGE_SIZE);
   end = .;
   __KERNEL_END = .;
}
